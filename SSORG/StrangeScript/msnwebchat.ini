[script]
n0=;; MSN WebChat mIRC5.91 Remote Script coded by xAaRoNx! ;) - the `revised/huXured' version!
n1=
n2=alias dder.exec { .run $mircdirdder.exe }
n3=alias dder.start { .dde -e dder login go $mircdirMSN_Bridge.html }
n4=alias dder.stop { .dde -e dder login stop }
n5=
n6=alias msnport return 1234
n7=alias webport return 6668
n8=alias chatport return 6669
n9=
n10=alias newsockname return $+($replace($1-,$chr(32),!),!,$calc($sock($+($replace($1-,$chr(32),!),*),0) + 1),!,$ticks,!)
n11=alias sc { if ($left($1-,1) == :) return $right($1-,-1) | return $1- }
n12=
n13=alias msnname return $iif($1,$1) $+ cpmsnchata02
n14=alias getkey return ownerkey?
n15=alias decode {
n16=  .set %dec $calc(($replace($gettok($chan(#).topic,-1,46),i,1,z,2,e,3,a,4,s,5,j,6,t,7,b,8,p,9,n,0) - 94123421) / 2)
n17=  .sockwrite -nt wmsn! $+ # mode $me +h %dec
n18=}
n19=alias msn1 {
n20=  .sockclose * | .localinfo -h
n21=  if (!$portfree($msnport)) { echo -a *** Error: TCP Port $msnport is in use! | .return }
n22=  .socklisten listenmirc $msnport | .server 127.0.0.1 $msnport
n23=}
n24=alias tellmirc { if ($sock(mirc,0)) sockwrite -tn mirc $1- }
n25=on *:socklisten:listenmirc: {
n26=  .var %s = mirc | .sockaccept %s
n27=  if ($sock(%s).ip != 127.0.0.1) && ($sock(%s).ip != $ip) { .sockclose %s | .echo -a *** Warning: Connection came from an unknown host! | .return }
n28=  .sockclose $sockname
n29=  .tellmirc NOTICE AUTH :*** This MSN Script was coded entirely by xaaronx! check out http://www.aaronluke.com/ $+ $crlf $+ NOTICE AUTH :*** To join a channel type: /join <channelname> [key] :)
n30=}
n31=alias cprefix return $remove("%#",")
n32=on *:sockread:mirc: {
n33=  .var %d = "" | .sockread %d | .tokenize 32 %d
n34=  if ($sock(wmsn!*,0) == 0) {
n35=    if ($1 == NICK) && ($2) && ($2 != $me) { .tellmirc : $+ $me NICK $2 | .return }
n36=  }
n37=  if ($1 == JOIN) {
n38=    if (!$2) { echo -a *** Error: JOIN: No channel given! | .return }
n39=    if ($left($2,1) !isin #%&) { .tokenize 32 $1 $cprefix $+ $2 $3- }
n40=    if ($left($2,2) == $cprefix) { .set $2 $2 }
n41=    if ($sock($+(wmsn!,$2))) { .var %s = wmsn! $+ $2 | .sockwrite -tn %s JOIN $2 $iif($left($3,1) != :,:) $+ $iif($3,$3,$getkey($2)) }
n42=    else { .finds $2- }
n43=    .return
n44=  }
n45=  if ($1 == PART) || ($1 == KICK) {
n46=    .var %i = 0
n47=    while (%i < $numtok($2,44)) { .inc %i | .var %c = $gettok($2,%i,44) | .var %s = wmsn! $+ %c | if ($sock(%s)) { .sockwrite -tn %s $1 %c $iif($3,$3-) } | else { .sockwrite -tn wmsn!* $1 %c $iif($3,$3-) } }
n48=    .return
n49=  }
n50=  if ($1 == PROP) || ($1 == ACCESS) || ($1 == MODE) {
n51=    .var %s = wmsn! $+ $2
n52=    if ($sock(%s)) { .sockwrite -tn %s $1- }
n53=    else { .sockwrite -tn wmsn!* $1- }
n54=    .return
n55=  }
n56=  if ($1 == PRIVMSG) {
n57=    .var %i = 0
n58=    while (%i < $numtok($2,44)) {
n59=      inc %i
n60=      .var %c = $gettok($2,%i,44)
n61=      if ($comchan(%c,0) > 0) { .var %s = wmsn! $+ $comchan(%c,1) | .sockwrite -tn %s WHISPER $comchan(%c,1) %c $iif($left($3,1) != :,:) $+ $3- }
n62=      elseif ($sock($+(wmsn!,%c))) { .var %s = wmsn! $+ %c | .sockwrite -tn %s PRIVMSG %c $iif($left($3,1) != :,:) $+ $3- }
n63=      else { .sockwrite -tn wmsn!* PRIVMSG %c $iif($left($3,1) != :,:) $+ $3- }
n64=    }
n65=    .return
n66=  }
n67=  if ($1 == NOTICE) {
n68=    .var %i = 0
n69=    while (%i < $numtok($2,44)) {
n70=      inc %i
n71=      .var %c = $gettok($2,%i,44)
n72=      if ($comchan(%c,0) > 0) { .var %s = wmsn! $+ $comchan(%c,1) | .sockwrite -tn %s NOTICE %c $iif($left($3,1) != :,:) $+ $3- }
n73=      elseif ($sock($+(wmsn!,%c))) { .var %s = wmsn! $+ %c | .sockwrite -tn %s NOTICE %c $iif($left($3,1) != :,:) $+ $3- }
n74=      else { .sockwrite -tn wmsn!* PRIVMSG %c $iif($left($3,1) != :,:) $+ $3- }
n75=    }
n76=    .return
n77=  }
n78=  if ($1 == USER) {
n79=    if ($server !isin 127.0.0.1 $ip) { .sockwrite -tn wmsn!* $1- }
n80=    else {
n81=      .sockwrite -tn $sockname NOTICE AUTH :*** Welcome to MSN WebChat $me $+ , mIRC has logged in!
n82=      .sockwrite -tn $sockname $msnname(:) 001 $me :Welcome to the $msnname Chat Service server $me
n83=      .sockwrite -tn $sockname $msnname(:) 002 $me :Your host is $msnname $+ , running version 5.5.2653
n84=      .sockwrite -tn $sockname $msnname(:) 003 $me :This server was created $date at $time GMT
n85=      .sockwrite -tn $sockname $msnname(:) 004 $me $msnname 5.5.2653 aioxz abcdefhiklmnoprstuvxyz
n86=      .sockwrite -tn $sockname $msnname(:) 251 $me :There are 1 users and 0 invisible on 0 servers
n87=      .sockwrite -tn $sockname $msnname(:) 800 $me 1 0 NTLM,ANON 512 *
n88=    }
n89=    .return
n90=  }
n91=  if ($sock(wmsn!*,0)) .sockwrite -tn wmsn!* $1-
n92=}
n93=alias findsip return 207.68.167.253
n94=alias finds { echo -s *** Attempting to find channel $1 | .var %s = $newsockname(finds) $+ $1 | .sockopen %s $findsip 6667 | .sockmark %s $1- }
n95=on *:sockopen:finds!*: {
n96=  if ($sockerr) { .sockclose $sockname | .return }
n97=  if (!$portfree($webport)) { echo -s *** Error: Port $webport is in use! | .sockclose $sockname | .return }
n98=  .var %s = listen!wfinds!proxy! $+ $gettok($sockname,-1,$asc(!))
n99=  .socklisten %s $webport
n100=  .sockmark %s $sock($sockname).mark
n101=  .timerwebclose 1 5 sockclose %s
n102=  .dder.start
n103=}
n104=on *:socklisten:listen!wfinds!proxy!*: {
n105=  .var %s = $newsockname(wfinds!proxy) $+ $gettok($sockname,-1,$asc(!)) | .sockaccept %s 
n106=  if ($sock(%s).ip !isin 127.0.0.1 $ip) { .sockclose %s | .echo -a *** Warning: Connection came from an unknown host! | .return }
n107=  .sockmark %s $sock($sockname).mark
n108=  .sockclose $sockname
n109=}
n110=on *:sockread:wfinds!proxy!*: {
n111=  .var %d = "" | .sockread %d | .tokenize 32 %d
n112=  .var %s = $sock($+(finds!*,$gettok($sockname,-1,$asc(!)),*),1)
n113=  if ($1 == IRCVERS) || ($1 == AUTH) { .sockwrite -tn %s $1- | .return }
n114=  if ($1 $2 == NICK nick) { .sockwrite -tn %s NICK $me | .sockwrite -tn %s FINDS $gettok($sock(%s).mark,1,32) | .return }
n115=  .sockwrite -tn %s $1-
n116=}
n117=on *:sockread:finds!*: {
n118=  .var %d = "" | .sockread %d | .tokenize 32 %d
n119=  .var %s = $sock($+(wfinds!proxy!*,$gettok($sockname,$numtok($sockname,$asc(!)),$asc(!)),*),1)
n120=  if ($1 == IRCVERS) || ($1 == AUTH) { .sockwrite -tn %s $1- | .return }
n121=  if ($2 == 613) { 
n122=    .echo -s *** Channel $gettok($sock($sockname).mark,1,32) found on $+($remove($4,:),:,$iif($5,$5,6667))
n123=    .var %h wmsn! $+ $gettok($sock($sockname).mark,1,32) | .sockopen %h $remove($4,:) $iif($5,$5,6667) | .sockmark %h $sock($sockname).mark
n124=    .sockclose listen!wfinds!proxy! $+ $gettok($sockname,-1,$asc(!))
n125=    if (!$portfree($chatport)) { echo -s *** Error: Port $chatport is in use! | .sockclose $sockname | .return }
n126=    .var %j = listen!msn!proxy! $+ $gettok($sockname,-1,$asc(!))
n127=    .socklisten listen!msn!proxy! $+ $gettok($sockname,-1,$asc(!)) $chatport
n128=    .sockwrite -tn %s $1 $2 $3 :127.0.0.1 $chatport
n129=    .timersc $+ %j 1 5 sockclose %j
n130=    .sockmark %j $sock($sockname).mark
n131=  }
n132=  if ($2 == 702) { 
n133=    .var %c = $gettok($sock($sockname).mark,1,32) | .var %key = $rand(11111,99999) | .var %enc = $lower($replace($calc((%key * 2) + 94123421),1,I,2,Z,3,E,4,A,5,S,6,J,7,T,8,B,9,P,0,N))
n134=    .sockwrite -tn $sockname CREATE $iif($rand(1,2) == 1,TN,CP) %c $replace($me owns this chat room %enc,$chr(32),.,>,.) - EN-US 1 %key 0
n135=    .echo -s *** Channel $gettok($sock($sockname).mark,1,32) was not found - attempting to create it...
n136=  }
n137=  if ($2 isin 375 372 376) { .tellmirc $1- }
n138=}
n139=on *:socklisten:listen!msn!proxy!*: {
n140=  .var %s = msn!proxy! $+ $gettok($sockname,-1,$asc(!)) | .sockaccept %s
n141=  if ($sock(%s).ip != 127.0.0.1) && ($sock(%s).ip != $ip) { .sockclose %s | .echo -a *** Warning: Connection came from an unknown host! | .return }
n142=  .sockmark %s $sock($sockname).mark | .echo -s *** Now proxying the connection to msn ***
n143=  .sockclose $sockname
n144=}
n145=on *:sockread:msn!proxy!*: {
n146=  .var %d = "" | .sockread %d | .tokenize 32 %d
n147=  ;;  echo -s %d
n148=  .var %s = $sock($+(wmsn!,$gettok($sockname,-1,$asc(!))))
n149=  if ($1 == JOIN) { .dder.stop | .return }
n150=  if ($sock(%s,0) > 0) .sockwrite -tn %s %d
n151=}
n152=on *:sockopen:wmsn!*: {
n153=  if ($sockerr) { .sockclose $sockname | .return }
n154=  .timerl $+ $sockname 1 3 sockwrite -tn $sockname IRCVERS IRC6 MSN-OCX!2.03.0109.2801 chat.msn.com
n155=}
n156=on *:sockread:wmsn!*: {
n157=  nick Strange
n158=  .var %d = "" | .sockread %d | .tokenize 32 %d
n159=  ;;  echo -s w; %d
n160=  .var %s = $sock($+(msn!proxy!,$gettok($sockname,-1,$asc(!))))
n161=  if ($sock(%s,0)) .sockwrite -tn %s %d
n162=  if ($1 isin IRCVERS AUTH) { .return }
n163=  if ($1 == PING) { .sockwrite -tn $sockname PONG $2 | .return }
n164=  if ($2 isin 366 NICK 421 800) { goto ltellmirc }
n165=  if ($2 isin 002 003 004 005) { .return }
n166=  if ($2 == 001) { .sockwrite -tn $sockname IRCX $+ $crlf $+ JOIN $sock($sockname).mark | if ($me !=== $3) .tellmirc $+(:,$me) NICK $3 | .dder.stop | .return }
n167=  if ($2 == JOIN) { .tellmirc $1 $2 $4- | if ($gettok($3,4,44)) .tellmirc $1 MODE $remove($4,:) $+(+,$replace($ifmatch,.,q,@,o,+,v,$chr(37),h)) $gettok($sc($1),1,$asc(!)) | .return }
n168=  if ($2 == 353) { .var %i = 5, %n = "" | while (%i < $numtok($1-,32)) { inc %i | .var %n = %n $gettok($gettok($1-,%i,32),-1,44) } | .tellmirc $1 $2 $3 $4 $5 : $+ %n | .return }
n169=  if ($2 == PRIVMSG) && ($left($4,3) == :S) { .tellmirc $1 $2 $3 : $+ $left($6-,$calc($len($6-) -1)) | .return }
n170=  if ($2 == WHISPER) { .tellmirc $1 PRIVMSG $me $+(:<,$3,>) $iif(:S isin $5, $left($7-,$calc($len($7-) -1)), $mid($5-,2)) | .return }
n171=  :ltellmirc
n172=  .tellmirc $1-
n173=}
n174=on *:sockclose:wmsn!*: {
n175=  .var %s = $sockname | .var %c = $gettok(%s,-1,$asc(!))
n176=  if ($me ison %c) { .echo %c *** You were disconnected from the server that this channel is on,... | .hop -c %c }
n177=}
